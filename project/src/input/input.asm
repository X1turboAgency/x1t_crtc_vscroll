;//---------------------------------------------------------------;
;	入力関連
;//---------------------------------------------------------------;

;bit0: 上
;bit1: 下
;bit2: 左
;bit3: 右
;bit5: ボタンA
;bit6: ボタンB??

;//---------------------------------------------------------------;
;//		ジョイスティックの初期化
;//---------------------------------------------------------------;
init_input:
	xor	a
	ld	(joy_w),a
	ld	(trg_w),a

	ld	(joy_w_2p),a
	ld	(trg_w_2p),a

	ret

;//---------------------------------------------------------------;
;//		ジョイスティックの読み込み
;//	現在のジョイスティックのデータと、トリガー情報を算出
;//	したものをワークにセットする。
;//
;//	joy_w:	現在のジョイスティック
;//	Areg,trg_w:	トリガー
;//---------------------------------------------------------------;
update_input:
	di

	ld		bc,01c00h	;1Pジョイパッド

	;1P
	ld		a,(joy_w)
	ld		e,a

	ld		d,14
	out		(c),d
	dec		b
	in		a,(c)
	cpl
	ld		(joy_w),a

	ld		d,a
	xor		e
	and		d
	ld		(trg_w),a

	;01c00h に戻す。
	inc		b

	;2P
	ld		a,(joy_w_2p)
	ld		e,a

	ld		d,15
	out		(c),d
	dec		b
	in		a,(c)
	cpl
	ld		(joy_w_2p),a

	ld		d,a
	xor		e
	and		d
	ld		(trg_w_2p),a

	ei

	ret

;bit0: 上
;bit1: 下
;bit2: 左
;bit3: 右
;bit4: 未使用
;bit5: トリガ1
;bit6: トリガ2
;bit7: 未使用

;1P側
trg_w:
	ds		1

joy_w:
	ds		1

;2P側(デバッグ用)
trg_w_2p:
	ds		1

joy_w_2p:
	ds		1

	END
